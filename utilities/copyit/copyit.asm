*LIST ON
;COPYIT VERSION 1.5/48K MEM-SIZE.
; ********************************************
; * COPYIT VERSION 1.5 (C) COPYRIGHT 19/4/82 *
; *       BY TRILOBYTE MICROCOMPUTING.       *
; *               NICK ANDREW.		     *
; ********************************************

;CHANGE LABELS TO CHANGE PROGRAM ORIGIN AND TABLE START.
;PROGST MUST BE AT LEAST 03E5 BYTES ABOVE START.
START	EQU	20480	;START OF PROGRAM.
PROGST	EQU	32768	;START OF PROGRAM TABLE.

;SUBSIDIARY LABELS...
TON	EQU	530
LEADER	EQU	662
READ	EQU	565
TOFF	EQU	504
WRLEDR	EQU	647
WRITE	EQU	612
GETKBD	EQU	0049H
MESAGE	EQU	28A7H
DELAY	EQU	0060H
VDUOUT	EQU	0033H
KEYBUF	EQU	16444
FLASH	EQU	022CH
VIDEO	EQU	3C3DH
CR	EQU	0DH

;SECURITY DEVICES...
*LIST OFF
	ORG	0
	DEFB	0
	ORG	0FFFDH
	DEFB	0
	ORG	15360
	DEFM	'SUPER COPIER (C) TRILOBYTE MICROCOMPUTING, 17/4/82'
	ORG	41E2H
	JP	START
*LIST ON
;COPYIT...
	ORG	START
	LD	SP,START-10
	LD	HL,06CCH
	PUSH	HL
	CALL	01C9H
	LD	HL,TITLE
	PUSH	HL
	CALL	MESAGE
	LD	HL,SOUR
	CALL	MESAGE
INPUT	CALL	GETKBD
	CP	49
	JR	Z,TAPE2
	CP	48
	JR	NZ,INPUT
	CALL	VDUOUT
	LD	A,15
	CALL	VDUOUT
	XOR	A
	LD	(START-1),A
	JR	PAST
TAPE2	CALL	VDUOUT
	LD	A,15
	CALL	VDUOUT
	LD	A,255
	LD	(START-1),A
PAST	LD	A,CR
	CALL	VDUOUT
	LD	HL,DEST
	CALL	MESAGE
INPUT2	CALL	GETKBD
	CP	49
	JR	NZ,TAPE1
	CALL	VDUOUT
	LD	A,15
	CALL	VDUOUT
	LD	A,255
	LD	(START-2),A
	JR	PASTA
TAPE1	CP	48
	JR	NZ,INPUT2
	CALL	VDUOUT
	LD	A,15
	CALL	VDUOUT
	XOR	A
	LD	(START-2),A
PASTA	LD	A,CR
	CALL	VDUOUT
PAST2	LD	HL,MESS
	CALL	MESAGE
	XOR	A
	LD	(KEYBUF),A
	CALL	GETKBD
	CP	1
	JP	Z,06CCH
	LD	A,(START-1)
	CALL	TON
	CALL	LEADER
	CALL	READ
	CP	85
	JP	Z,SYSTEM
	CP	211
	JR	Z,HALF
	CALL	TOFF
	LD	HL,NOTPR
	CALL	MESAGE
	JR	PAST2
HALF	CALL	READ
	CP	211
	JR	Z,BASIC
	LD	HL,VIDEO
	LD	(HL),'E'
	LD	HL,PROGST
	LD	(HL),211
	LD	B,5
RNAME	INC	HL
	LD	(HL),A
	CALL	READ
	DJNZ	RNAME
	INC	HL
	LD	(HL),A
NUMBA	LD	B,5
RNUMB	CALL	READ
	INC	HL
	LD	(HL),A
	CP	26
	JR	Z,FINIS1
	DJNZ	RNUMB
	CALL	FLASH
TEXT	CALL	READ
	INC	HL
	LD	(HL),A
	CP	13
	JR	NZ,TEXT
	JR	NUMBA
FINIS1	LD	(START-4),HL
	LD	BC,0
	CALL	DELAY
	CALL	DELAY
	CALL	TOFF
	LD	HL,SOURCE
	CALL	MESAGE
XYZ	LD	HL,PROGST+1
	LD	B,6
PRINT	LD	A,(HL)
	INC	HL
	CALL	VDUOUT
	DJNZ	PRINT
	LD	HL,ENDNAM
	CALL	MESAGE
	JP	DECIDE
BASIC	LD	HL,VIDEO
	LD	(HL),'B'
	LD	HL,PROGST
	LD	(HL),211
	INC	HL
	LD	(HL),211
	INC	HL
	CALL	READ
	LD	(HL),A
	INC	HL
	CALL	READ
	LD	(HL),A
	INC	HL
TIXT	CALL	READ
	LD	(HL),A
	INC	HL
	CP	0
	JR	Z,ZERO
GO2	CALL	READ
	LD	(HL),A
	INC	HL
LOOP2	CALL	READ
	LD	(HL),A
	INC	HL
	CP	0
	JR	NZ,LOOP2
	CALL	FLASH
	JR	TIXT
ZERO	CALL	READ
	LD	(HL),A
	INC	HL
	CP	0
	JR	NZ,GO2
	DEC	HL
	LD	(START-4),HL
	LD	BC,0
	CALL	DELAY
	CALL	DELAY
	CALL	TOFF
	LD	HL,BASICT
	CALL	MESAGE
	LD	A,(PROGST+3)
	CALL	VDUOUT
	LD	HL,ENDNAM
	CALL	MESAGE
	JR	DECIDE
SYSTEM	LD	HL,VIDEO
	LD	(HL),'S'
	LD	HL,PROGST
	LD	(HL),A
	INC	HL
	LD	B,6
LOOP4	CALL	READ
	LD	(HL),A
	INC	HL
	DJNZ	LOOP4
LOOP6	CALL	READ
	LD	(HL),A
	INC	HL
	CP	120
	JR	Z,GETOUT
	CALL	READ
	LD	(HL),A
	INC	HL
	LD	B,A
	CALL	READ
	LD	(HL),A
	INC	HL
	CALL	READ
	LD	(HL),A
	INC	HL
LOOP5	CALL	READ
	LD	(HL),A
	INC	HL
	DJNZ	LOOP5
	CALL	READ
	LD	(HL),A
	INC	HL
	CALL	FLASH
	JR	LOOP6
GETOUT	CALL	READ
	LD	(HL),A
	INC	HL
	CALL	READ
	LD	(HL),A
	LD	(START-4),HL
	LD	BC,0
	CALL	DELAY
	CALL	DELAY
	CALL	TOFF
	LD	HL,SYSTAP
	CALL	MESAGE
	JP	XYZ
DECIDE	LD	HL,DEC
	CALL	MESAGE
	XOR	A
	LD	(KEYBUF),A
	CALL	GETKBD
	CP	32
	JP	NZ,PAST2
	LD	A,(START-2)
	CALL	TON
	LD	BC,0
	CALL	DELAY
	CALL	DELAY
	CALL	WRLEDR
	LD	HL,(START-4)
	INC	HL
	LD	DE,PROGST
	OR	A
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,PROGST
LOOP3	LD	A,(HL)
	INC	HL
	DEC	BC
	CALL	WRITE
	LD	A,B
	OR	C
	JR	NZ,LOOP3
	XOR	A
	CALL	0261H
	CALL	0261H
	LD	BC,0
	CALL	DELAY
	CALL	DELAY
	CALL	TOFF
	LD	HL,WRITEN
	CALL	MESAGE
	JP	PAST2
;MESSAGE TABLE START...
*LIST OFF
TITLE	DEFM	'<COPYIT> SUPER COPIER VERSION 1.5 COPYRIGHT 19/4/82'
	DEFB	CR
	DEFM	'BY TRILOBYTE MICROCOMPUTING, NICK ANDREW.'
	DEFB	CR
	DEFB	CR
	DEFB	0
SOUR	DEFM	'SOURCE TAPE UNIT NO. (0 OR 1)? '
	DEFB	14
	DEFB	0
DEST	DEFM	'DESTINATION TAPE UNIT NO. (0 OR 1)? '
	DEFB	14
	DEFB	0
MESS	DEFB	CR
	DEFM	'HIT ANY KEY TO LOAD PROGRAM'
	DEFB	CR
	DEFM	'OR <BREAK> TO RETURN TO BASIC.'
	DEFB	CR
	DEFB	CR
	DEFB	0
NOTPR	DEFM	'** DATA ON TAPE NOT BASIC, SOURCE OR OBJECT CODE!! **'
	DEFB	CR
	DEFB	CR
	DEFB	0
SOURCE	DEFM	'EDTASM SOURCE TAPE: NAME IS <'
	DEFB	0
ENDNAM	DEFM	'>.'
	DEFB	CR
	DEFB	0
WRITEN	DEFM	'TAPE WRITE COMPLETED.'
	DEFB	CR
	DEFB	0
BASICT	DEFM	'BASIC TAPE: NAME IS <'
	DEFB	0
DEC	DEFM	'HIT <SPACEBAR> TO COPY PROGRAM'
	DEFB	CR
	DEFM	'OR ANY OTHER KEY TO ABORT WRITE.'
	DEFB	CR
	DEFB	0
SYSTAP	DEFM	'SYSTEM TAPE: NAME IS <'
	DEFB	0
	DEFB	0
	END	0674H
