00010   CLS
00015   PRINT "CONVERT/BAS: CHANGES SPACES IN EDTASM SOURCE FILES TO TABS."
00020   : REM ' CONVERT/BAS CHANGES SPACES IN EDTASM SOURCE CODE TO TABS.
00030   CLEAR 500
00031   LINE INPUT "WORK ON ORIGINAL: ";QU$
00040   LINE INPUT "EDTASM FILESPEC: ";A$
00042   IF LEFT$(QU$,1) <> "N" THEN 50
00045   LINE INPUT "WORK FILESPEC: ";E$: CMD "COPY " + A$ + " TO " + E$:A$ = E$: OPEN "R",1,A$: FIELD 1,1ASTY$,6ASYT$: GET 1,1
00046   I = INSTR(A$,"/"): IF I = 0 THEN LSET YT$ = A$: PUT 1,1: CLOSE : GOTO 50
00047   LSET YT$ = LEFT$(A$,I - 1): PUT 1,1: CLOSE
00050   OPEN "R",1,A$
00060   FIELD 1,128 AS B$,128 AS C$
00070   T = 0: GET 1,1:RE = 1:D$ = B$:E$ = C$
00080   GOSUB 190
00090   IF O <> 211 THEN PRINT "NOT AN EDTASM FILE": CLOSE : RUN 30
00100   PRINT "EDTASM FILE: NAME IS '";: FOR U = 1 TO 6: GOSUB 190: PRINT CHR$(O);: NEXT U: PRINT "'."
00110   FOR U = 1 TO 5: GOSUB 190: IF O = 26 THEN 260 : ELSE PRINT CHR$(O - 128);: NEXT : GOSUB 190: PRINT " ";
00120   GOSUB 190
00130   IF O = 32 THEN O = 9: GOSUB 230: PRINT ,: GOTO 150
00140   PRINT CHR$(O);
00150   IF O = 39 OR O = 59 THEN GOTO 180
00160   IF O = 13 THEN GOTO 110
00170   GOTO 120
00180   GOSUB 190: PRINT CHR$(O);: IF O = 13 THEN GOTO 110 : ELSE GOTO 180
00190   T = T + 1: IF T = 257 THEN T = 1: LSET B$ = D$: LSET C$ = E$: PUT 1,RE:RE = RE + 1: IF RE = LOF(1) + 1 THEN CLOSE : END : ELSE GET 1,RE:D$ = B$:E$ = C$
00200   IF T> 128 THEN 220
00210   O = ASC(MID$(B$,T,1)): RETURN
00220   O = ASC(MID$(C$,T - 128,1)): RETURN
00230   IF T> 128 THEN 250
00240   MID$(D$,T,1) = CHR$(O): RETURN
00250   MID$(E$,T - 128,1) = CHR$(O): RETURN
00260   PRINT "END OF FILE":T = 256: GOTO 190
00999   : REM ' LSET YT$=A$ : PUT 1,1:CLOSE
