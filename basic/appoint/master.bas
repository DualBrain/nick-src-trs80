00100   : REM ' MASTER/BAS: 09/20/83 COMBINATION OF APPOINT AND REMINDER.
00110   : REM ' **************************************************
00120   : REM ' ** REMINDER: PROVIDES MESSAGE BULLETINS FOR THE **
00130   : REM ' ** USER AT VARIOUS TIMES: COMPUTER ALARM CLOCK  **
00140   : REM ' ** AND BULLETIN BOARD.                          **
00150   : REM ' ** MACHINE: TRS80/SYSTEM 80 32K OR 48K (1 DISK) **
00160   : REM ' ** OPERATING SYSTEM: NEWDOS/80 VERSION 2 (ONLY) **
00170   : REM ' **                BY NICK ANDREW                **
00180   : REM ' **                 'BRIERDENE'                  **
00190   : REM ' **        77 WINDSOR ROAD VINEYARD 2765.        **
00200   : REM ' **               TEL: 02-627-2386               **
00210   : REM ' **             20TH SEPTEMBER, 1983             **
00220   : REM ' **************************************************
00230   SAVE "MASTER/BAS":: REM ' SAVE PROGRAM: REMOVE THIS!!!
00240   CLS : CMD "R": CMD "CLOCK N":: REM ' COLD START LINE NUMBER.
00250   : REM ' FIND HIMEM VALUE FOR SOUND SUBROUTINE
00260   HI = PEEK(&H4049) + 256 * PEEK(&H404A):: REM ' TOP OF MEMORY
00270   LE = 040:: REM ' LENGTH OF MACHINE LANGUAGE SUBROUTINE.
00280   HI = HI - LE - 1:: REM ' NEW HIMEM VALUE
00290   H1 = INT(HI / 256):H2 = HI - H1 * 256
00300   DIM H(3):: REM ' THREE SEPARATE HIMEM VALUES USED BY DISK BASIC.
00310   FOR LOOP = 1 TO 3: READ H(LOOP): NEXT LOOP
00320   FOR LOOP = 1 TO 3
00330   POKE H(LOOP),H2: POKE H(LOOP) + 1,H1:: REM ' POKE IN NEW HIMEM
00340   NEXT LOOP
00350   CLEAR 6000:: REM ' CLEAR STRING SPACE AFTER SETTING NEW HIMEM.
00360   GOSUB 1000:: REM ' SETUP PROGRAM & FILES ETC...
00370   : REM ' JUMP HERE FOR WARM START.
00380   END
01000   : REM ' SETUP PROGRAM
01010   GOSUB 20000:: REM ' POKE IN SUBROUTINES
01020   GOSUB 20200:: REM ' GET MACHINE TYPE FOR TONE GENERATOR
01030   GOSUB 21600:: REM ' SIGN ON HEADING
01040   GOSUB 20400:: REM ' OPEN DATA FILES
01050   GOSUB 21000:: REM ' READ IN DAY OF WEEK DATA
01060   GOSUB 21200:: REM ' GET INITIAL TIME & DATE
01070   RETURN
20000   : REM ' POKE IN SUBROUTINES
20010   DEF FN A(A) = A + 65536 *(A> 32767)
20020   LE = 040:: REM ' LENGTH OF SUBROUTINE
20030   RG = PEEK(&H4049) + 256 * PEEK(&H404A) + 1:: REM ' ORIGIN OF CODE
20040   RESTORE : READ A,A,A:: REM ' BYPASS FIRST THREE DATA ITEMS.
20050   FOR LOOP = 0 TO LE - 1: READ DD
20060   O1 = RG + LOOP:: REM ' ABSOLUTE DATA LOCATION
20070   O2 = FN A(O1):: REM ' CONVERT TO SIGNED VALUE
20080   POKE O2,DD
20090   NEXT LOOP
20100   O3 = FN A(RG):: REM ' SIGNED VALUE
20110   DEF USR 0 = O3:: REM ' START ADDRESS OF BEEP ROUTINE
20120   RETURN
20200   : REM ' GET MACHINE TYPE FOR TONE GENERATOR
20210   T1 =(PEEK(&H0105) = &H4D):: REM ' 0=SYSTEM-80,-1=TRS-80
20220   IF T1 THEN TY = 0: RETURN
20230   TY = 1024
20240   T2 =(PEEK(&H3000) = &HFF):: REM ' 0=EPROM,-1=NO EPROM
20250   IF T2 THEN OUT 254,16: GOTO 20280
20260   TY = 0
20270   : REM ' TY=0: TRS-80 SOUND, TY=1: SYSTEM-80 SOUND.
20280   RETURN
20400   : REM ' OPEN DATA FILES
20410   : REM ' DEFAULT FILE NAMES ARE REMIND1/DAT AND REMIND2/DAT
20420   PRINT "INPUT NAMES OF FILES OR <ENTER> FOR DEFAULT NAMES:"
20430   LINE INPUT "FILE NUMBER 1: ";F1$
20440   A = USR 0(TY + 1)
20450   IF F1$ = "" THEN F1$ = "REMIND1/DAT"
20460   LINE INPUT "FILE NUMBER 2: ";F2$
20470   A = USR 0(TY + 1)
20480   IF F2$ = "" THEN F2$ = "REMIND2/DAT"
20490   IF INSTR(F1$,"/") = 0 THEN F1$ = F1$ + "/DAT"
20500   IF INSTR(F2$,"/") = 0 THEN F2$ = F2$ + "/DAT"
20510   PRINT "NAME OF REMINDER TIMES FILE: ";F1$
20520   PRINT "NAME OF MESSAGES FILE:       ";F2$
20530   A$ = "CONTINUE": GOSUB 20600:: REM ' WAIT FOR KEY HIT
20540   OPEN "R",1,F1$,41
20550   OPEN "R",2,F2$,64
20560   FIELD 1,1 AS MN$,8 AS MI$,3 AS HR$,1 AS DA$,7 AS D1$,                   7 AS D2$,7 AS D3$,7 AS D4$
20570   FIELD 2,64 AS M$
20580   RETURN
20600   : REM ' WAIT FOR KEY HIT
20610   PRINT "HIT ANY KEY TO ";A$
20620   POKE &H4099,0:: REM ' CLEAR INKEY$ BUFFER
20630   I$ = INKEY$ : IF I$ = "" THEN 20630
20640   IF TY THEN OUT 254,16 0x09 :: REM ' SELECT 2ND CASSETTE FOR SYS/80
20650   A = USR 0(TY + 1)
20660   DE = 0.75: GOSUB 25200:: REM ' DELAY 0.75 SECONDS.
20670   RETURN
20800   : REM ' DATA STATEMENTS FOLLOW:
20810   DATA 16457,16561,16598:: REM ' DISK BASIC HIMEM LOCATIONS.
20820   DATA 205,127,010,124,230,004,103,014,100,062,001,180,211
20830   DATA 255,006,050,016,254,062,000,180,211,255,006,050,016
20840   DATA 254,013,032,235,001,000,010,205,096,000,045,032,224
20850   DATA 201:: REM ' OBJECT CODE FOR SOUND ROUTINE.
20860   DATA SUNDAY,MONDAY,TUESDAY,WEDNESDAY
20870   DATA THURSDAY,FRIDAY,SATURDAY
20880   DATA 0,3,3,6,1,4,6,2,5,0,3,5:: REM ' DATA FOR EACH MONTH.
21000   : REM ' READ IN DAY OF WEEK DATA
21010   DIM DN$(6)
21020   FOR LOOP = 0 TO 6: READ DN$(LOOP): NEXT LOOP
21030   DIM DM(12):: REM ' FOR MONTH DATA
21040   FOR LOOP = 1 TO 12: READ DM(LOOP): NEXT LOOP
21050   RETURN
21200   : REM ' GET INITIAL TIME & DATE
21210   PRINT "GETTING INITIAL TIME AND DATE:"
21220   LINE INPUT "INPUT DATE IN MM/DD/YY FORMAT: ";ID$
21230   IF ID$ <> "" THEN GOTO 21320
21240   DT$ = LEFT$(TIME$ ,8)
21250   PRINT "NO DATE SUPPLIED. CURRENT DATE IS: ";DT$;"."
21260   PRINT "USE DATE IN MEMORY (Y/N): ";
21270   CO$ = "YN"
21280   DE = 0:P1 = 0: GOSUB 25000
21290   PRINT MID$(CO$,NUM,1):DE = 1: GOSUB 25200
21300   IF NUM = 2 THEN GOTO 21210
21310   ID$ = LEFT$(TIME$ ,8)
21320   IF LEN(ID$) <> 8 THEN GOTO 21230
21330   A = USR 0(TY + 1)
21340   PRINT "INPUT TIME AT NEXT EVEN MINUTE:"
21350   LINE INPUT "INPUT TIME IN HH:MM FORMAT: ";IT$
21360   IF LEN(IT$) <> 5 OR INSTR(IT$,":") <> 3 THEN 21350
21370   A = USR 0(TY + 1)
21380   CMD "DATE " + ID$
21390   CMD "TIME " + IT$ + ":01"
21400   CMD "CLOCK Y"
21410   PRINT "HIT <SPACE> WHEN SEC=00"
21420   IF PEEK(&H3840) <> 128 THEN GOTO 21420
21430   POKE 16448,39: POKE 16449,0
21440   A = USR 0(TY + 1)
21450   GOSUB 21480:: REM ' GET DAY OF WEEK
21460   PRINT "INITIALISING: ";D$;" "; TIME$
21470   RETURN
21480   : REM ' GET DAY OF WEEK
21490   T$ = LEFT$(TIME$ ,8)
21500   T1$ = LEFT$(T$,2):T2$ = MID$(T$,4,2):T3$ = RIGHT$(T$,2)
21510   M1 = VAL(T1$):D1 = VAL(T2$):Y1 = VAL(T3$)
21520   IF M1> 2 THEN Y2 = Y1 : ELSE Y2 = Y1 - 1
21530   Y3 = INT(Y2 / 4):: REM ' NUMBER OF LEAP YEARS
21540   D2 = Y3 + DM(M1) + Y1 + D1
21550   D3 = INT(D2 / 7)
21560   D4 = D2 - D3 * 7
21570   D = D4:D$ = DN$(D4)
21580   RETURN
21600   : REM ' SIGN ON HEADING
21610   CLS
21620   IF FL = 0 THEN FL = 1:A = USR 0(TY + 4) : ELSE A = USR 0(TY + 1)
21630   FOR LOOP = 0 TO 63: POKE 15360 + LOOP,140
21640   POKE 16320 + LOOP,140: NEXT LOOP
21650   C1 = 188:C2 = 143:C3 = 191:: REM ' CHARACTERS FOR POKING
21660   POKE 15360,C1: POKE 16320,C2: POKE 15423,C1: POKE 16383,C2
21670   FOR LOOP = 1 TO 14: POKE 15360 + 64 * LOOP,C3
21680   POKE 15423 + 64 * LOOP,C3: NEXT LOOP
21690   A1$ = "TRS-80/SYSTEM-80 COMPUTERISED REMINDER PROGRAM"
21700   L1 = LEN(A1$)
21710   PRINT @96 - L1 / 2,A1$;
21720   C(1) = 153:C(0) = 166
21730   FOR LOOP = 1 TO 62
21740   T1 = LOOP + 15424
21750   T2 = T1 + 64
21760   IF PEEK(T1) <> 32 THEN POKE T2,C(D)
21770   D = NOT D AND 1:: REM ' TOGGLE BETWEEN 0 AND 1
21780   NEXT LOOP
21790   P1 = 266:P2 = 0
21800   A$ = "MENU ONE. PRESS:": GOSUB 25600
21810   A$ = "<C>  TO START PROGRAM": GOSUB 25600
21820   A$ = "<D>  FOR DISK DIRECTORY": GOSUB 25600
21830   A$ = "<E>  TO EXIT PROGRAM": GOSUB 25600
21840   A$ = "<F>  FOR DATA FILE DIRECTORY": GOSUB 25600
21850   A$ = "<I>  FOR PROGRAM INSTRUCTIONS": GOSUB 25600
21860   A$ = "<K>  TO KILL A DATA FILE": GOSUB 25600
21870   CO$ = "CDEFIK"
21880   DE = 0.5:P1 = 330:P2 = 64:P3 = 2: GOSUB 25000
21890   ON NUM GOTO 21900,21910,21950,21960,22120,21980
21900   CLS : RETURN
21910   CMD "DIR"
21920   PRINT : PRINT :A$ = "RETURN TO MENU"
21930   GOSUB 20600
21940   GOTO 21600
21950   CLS : PRINT "BYTE FOR NOW...": END
21960   CMD "DIR A /DAT"
21970   GOTO 21920
21980   CMD "DIR A /DAT"
21990   PRINT : PRINT "YOU MAY KILL ANY ONE OF THESE FILES."
22000   LINE INPUT "FILE (TO KILL): ";FI$
22010   IF FI$ <> "" THEN GOTO 22040
22020   PRINT "NO FILE NAME GIVEN: ABORTING COMMAND."
22030   A$ = "ABORT COMMAND": GOSUB 20600: GOTO 21600
22040   IF INSTR(FI$,"/") = 0 THEN FI$ = FI$ + "/DAT"
22050   KILL FI$:: REM ' KILL THE FILE
22060   A = USR 0(TY + 1)
22070   GOTO 21600
22080   PRINT : PRINT :A$ = "RETURN TO MENU": GOSUB 20600: GOTO 21600
22090   IF I$ <> "F" THEN GOTO 22110
22100   CMD "DIR A /DAT": GOTO 22080
22110   IF I$ <> "I" THEN GOTO 21870
22120   : REM ' PROGRAM INSTRUCTIONS.
22130   CLS : PRINT "PROGRAM INSTRUCTIONS"
22140   GOTO 21600
25000   : REM ' MENU COMMAND SELECTION SUBROUTINE.
25010   POKE &H4099,0:: REM ' CLEAR INKEY$ BUFFER
25020   I$ = INKEY$ : IF I$ = "" THEN GOTO 25020
25030   L = LEN(CO$):NUM = 0
25040   FOR LOOP = 1 TO L
25050   CH$ = MID$(CO$,LOOP,1)
25060   IF CH$ = I$ THEN NUM = LOOP
25070   NEXT LOOP
25080   IF NUM = 0 THEN GOTO 25010
25090   A = USR 0(TY + 1)
25100   IF P1 <> 0 THEN GOSUB 25400
25110   IF DE <> 0 THEN GOSUB 25200
25120   RETURN
25200   : REM ' DELAY SUBROUTINE
25210   FOR LOOP = 1 TO 250 * DE
25220   NEXT LOOP
25230   RETURN
25400   : REM ' SIGNAL SELECTION.
25410   PO = P1 + P2 *(NUM - 1) + 15360
25420   FOR L2OOP = 1 TO 5
25430   POKE PO,32
25440   IF P3 <> 0 THEN POKE PO + P3,32
25450   DE = 0.2: GOSUB 25200
25460   POKE PO,62
25470   IF P3 <> 0 THEN POKE PO + P3,60
25480   DE = 0.2: GOSUB 25200
25490   NEXT L2OOP
25500   RETURN
25600   : REM ' PRINT NEXT LINE OF MENU
25610   IF P2 = 0 THEN P2 = P1
25620   PRINT @P2,A$;
25630   P2 = P2 + 64
25640   RETURN
29999   CLS : RETURN
