;BB3.asm: BB code file #3, 18-Jan-88
;
TOPIC_PRINT
	LD	(TEMP_TOPIC),A
	LD	A,0
	CALL	TOP_ADDR
	CALL	TOP_PRT_1
	LD	A,(TEMP_TOPIC)
	AND	0E0H
	OR	A
	RET	Z
;
	PUSH	AF
	LD	A,'>'
	CALL	PUT
	POP	AF
	CALL	TOP_INT
	CALL	TOP_ADDR
	CALL	TOP_PRT_1
;
	LD	A,(TEMP_TOPIC)
	AND	0FCH
	LD	B,A
	AND	1CH
	RET	Z
	LD	A,B
	PUSH	AF
	LD	A,'>'
	CALL	PUT
	POP	AF
	CALL	TOP_INT
	CALL	TOP_ADDR
	CALL	TOP_PRT_1
;
	LD	A,(TEMP_TOPIC)
	OR	A
	AND	3
	RET	Z
	LD	A,'>'
	CALL	PUT
	LD	A,(TEMP_TOPIC)
	CALL	TOP_INT
	CALL	TOP_ADDR
	CALL	TOP_PRT_1
	RET
;
TOP_PRT_1
	LD	A,(HL)
	CP	CR
	RET	Z
	OR	A
	JR	NZ,TP1_1
	LD	A,'*'
	CALL	PUT
	RET
TP1_1
	CALL	PUT
	INC	HL
	JR	TOP_PRT_1
;
TOPIC_DOWN
	LD	B,A
	OR	A
	JR	NZ,TD_1
	LD	A,C		;level 0 to 1.
	AND	7
	RRCA
	RRCA
	RRCA
	CP	A
	RET
TD_1	LD	A,B
	AND	1CH
	JR	NZ,TD_2
	LD	A,C		;level 1 to 2.
	AND	7
	RLCA
	RLCA
	OR	B
	CP	A
	RET
TD_2	LD	A,B
	AND	3
	RET	NZ
	LD	A,C
	AND	3
	OR	B
	CP	A
	RET
;
TOPIC_UP
	LD	B,A
	AND	3
	JR	Z,TU_1
	LD	A,B
	AND	0FCH
	RET
TU_1	LD	A,B
	AND	1CH
	JR	Z,TU_2
	LD	A,B
	AND	0E0H
	RET
TU_2	XOR	A
	RET
;
LIST_CMD
	CALL	GET_CHAR
	CP	CR
	JP	NZ,BADSYN
	XOR	A
	LD	(TOPIC_CNT),A
	LD	HL,M_UNDER
	CALL	MESS
	LD	A,(MY_TOPIC)
	CALL	TOPIC_PRINT
	CALL	PUTCR
	LD	B,7
	LD	A,(MY_LEVEL)
	CP	3
	JR	Z,LC_4
	CP	2
	JR	C,LC_1
	LD	B,3
LC_1	LD	C,1
LC_2	PUSH	BC
	LD	A,(MY_TOPIC)
	CALL	TOPIC_DOWN
	LD	(TEMP_TOPIC),A
	CALL	TOP_INT
	CALL	TOP_ADDR
	LD	A,(HL)
	OR	A
	JR	Z,LC_3
	LD	A,1
	LD	(TOPIC_CNT),A
	LD	A,(TEMP_TOPIC)
	CALL	TOPIC_PRINT
	CALL	PUTCR
LC_3	POP	BC
	INC	C
	DJNZ	LC_2
LC_4	LD	A,(TOPIC_CNT)
	OR	A
	JP	NZ,MAIN
	LD	HL,M_NOBELO
	CALL	MESS
	JP	MAIN
;
HDR_SCAN
;
	LD	HL,M_MSG2
	CALL	MESS
	LD	HL,(MSG_NUM)
	CALL	PRINT_NUMB
;
	CALL	BGETC		;The flags?
	CALL	BGETC		;Dummy byte?
;
	LD	HL,M_SNDR
	CALL	MESS
	CALL	TXT_GET_PUT_NCR
	LD	HL,M_RCVR
	CALL	MESS
	CALL	TXT_GET_PUT_NCR
HS_08	LD	A,(HDR_FLAG)	;And privacy stuff
	BIT	FM_PRIVATE,A
	JR	Z,HS_09
	LD	HL,M_P
	CALL	MESS
HS_09
	LD	HL,M_DATE
	CALL	MESS
	CALL	TXT_GET_PUT_NCR
	LD	HL,M_MSGTOP
	CALL	MESS
	LD	A,(HDR_TOPIC)
	CALL	TOPIC_PRINT
	LD	HL,M_SUBJ
	CALL	MESS
	CALL	TXT_GET_PUT_NCR
	CALL	PUTCR
	RET
;
CHK_USERS
;name is in NAME_BUFF terminated by CR.
;move to USN_BUFF.
	LD	HL,NAME_BUFF
	LD	DE,USN_BUFF
CU_1	LD	A,(HL)
	LD	(DE),A
	CP	CR
	JR	Z,CU_2
	INC	HL
	INC	DE
	JR	CU_1
CU_2	LD	HL,USN_BUFF
	CALL	USER_SEARCH
	LD	HL,(UF_UID)
	LD	(US_NUM),HL
	RET	;Z or NZ (even with errors!)
;
OPT_CMD
	CALL	GET_CHAR
	CP	CR
	JP	NZ,BADSYN
	CALL	IF_CHAR
	JR	Z,OC_X3
;print out current options.
OC_0A	LD	HL,M_OPTIONS
	CALL	MESS
	LD	HL,OPTIONS
	BIT	FO_CURR,(HL)
	JR	Z,OC_1
	LD	HL,M_CURR
	CALL	MESS
	LD	HL,OPTIONS
OC_1	BIT	FO_LOWR,(HL)
	JR	Z,OC_2
	LD	HL,M_LOWR
	CALL	MESS
	LD	HL,OPTIONS
OC_2
	BIT	FO_NORM,(HL)
	JR	Z,OC_3
	LD	HL,M_NORM
	CALL	MESS
	LD	HL,OPTIONS
OC_3	BIT	FO_EXP,(HL)
	JR	Z,OC_4
	LD	HL,M_EXP
	CALL	MESS
	LD	HL,OPTIONS
OC_4
;print up menu.
OC_X2	LD	HL,MENU_OPT
	CALL	MENU
	LD	HL,PMPT_OPT
	CALL	GET_STRING
OC_X3	CALL	GET_CHAR
	CP	CR
	JP	Z,MAIN
	CP	'1'
	JR	Z,SET_CURR
	CP	'2'
	JR	Z,SET_LOWR
	CP	'3'
	JR	Z,SET_NORM
	CP	'4'
	JR	Z,SET_EXP
	JP	MAIN
;
SET_CURR
	CALL	GET_CHAR
	CP	CR
	JR	NZ,OC_X2
	LD	HL,OPTIONS
	SET	FO_CURR,(HL)
	RES	FO_LOWR,(HL)
	CALL	INFO_SETUP
OC_X4	LD	HL,M_OK
	CALL	MESS
	JP	MAIN
;
SET_LOWR
	CALL	GET_CHAR
	CP	CR
	JR	NZ,OC_X2
	LD	HL,OPTIONS
	RES	FO_CURR,(HL)
	SET	FO_LOWR,(HL)
	CALL	INFO_SETUP
	JR	OC_X4
;
SET_NORM
	CALL	GET_CHAR
	CP	CR
	JR	NZ,OC_X2
	LD	HL,OPTIONS
	RES	FO_EXP,(HL)
	SET	FO_NORM,(HL)
	JR	OC_X4
;
SET_EXP
	CALL	GET_CHAR
	CP	CR
	JR	NZ,OC_X2
	LD	HL,OPTIONS
	RES	FO_NORM,(HL)
	SET	FO_EXP,(HL)
	JR	OC_X4
;
SET_MASK
	LD	A,0FFH		;all!
	LD	(TOPIC_MASK),A
	LD	A,(OPTIONS)
	BIT	FO_LOWR,A
	RET	Z		;if LOWER not selected.
	LD	A,(MY_LEVEL)
	LD	E,A
	LD	D,0
	LD	HL,MASK_DATA
	ADD	HL,DE
	LD	A,(HL)
	LD	(TOPIC_MASK),A
	RET
;
CLEAN_DISCON			;A clean disconnect.
	CALL	CLOSE_ALL
	LD	A,0
	JP	TERM_DISCON	;pass a disconnect.
;
SPEC_CMD
	CALL	GET_CHAR
	CP	CR
	JP	NZ,BADSYN
	LD	HL,MENU_SPEC
	CALL	MENU
	LD	HL,PMPT_SPEC
	CALL	GET_STRING
SPEC_1	CALL	GET_CHAR
	CP	CR
	JP	Z,MAIN
	CP	' '
	JR	Z,SPEC_1
	CALL	TO_UPPER_C
;
	PUSH	AF
	CALL	GET_CHAR
	POP	AF
;
	CP	'#'
	JP	Z,MAIN
	CP	'C'		;Create subtopic
	JP	Z,CREATE_CMD
	CP	'D'		;Delete this topic
	JP	Z,DELTOP_CMD
	CP	'F'		;Forward msgs to user
	JP	Z,FORWARD_CMD
	CP	'M'
	JP	Z,MOVMSG_CMD
	CP	'R'
	JP	Z,RESEND_CMD	;In bb6
	JP	BADSYN
;
MOVEMESSAGE
	LD	A,(MSG_FOUND)
	OR	A
	JR	NZ,MMSG_1
	XOR	A
	LD	(MOVE_QUERY),A
	LD	HL,M_MVQRY
	CALL	YES_NO
	CP	'N'
	JR	Z,MMSG_1
	CP	'Q'
	JR	Z,MMSG_Q
	LD	A,1
	LD	(MOVE_QUERY),A
MMSG_1
	CALL	GET_$2
	CP	1
	JR	Z,MMSG_Q
;make sure message is TO me or FROM me or SYSOP.
	CALL	IF_SYSOP
	JR	NZ,MMSG_2
	LD	DE,(USR_NUMBER)
	LD	HL,(HDR_SNDR)
	OR	A
	SBC	HL,DE
	JR	Z,MMSG_2
	LD	HL,(HDR_RCVR)
	OR	A
	SBC	HL,DE
	JR	Z,MMSG_2
	LD	HL,M_MSG2
	CALL	MESS
	LD	HL,(MSG_NUM)
	CALL	PRINT_NUMB
	LD	HL,M_NTFRYO
	CALL	MESS
	RET
;
MMSG_2
	CALL	TEXT_POSN
	CALL	HDR_PRNT
	LD	A,(MOVE_QUERY)
	OR	A
	JR	Z,MMSG_3
	LD	HL,M_MOVEIT
	CALL	YES_NO
	CP	'Y'
	JR	Z,MMSG_3
	CP	'N'
	RET	Z
MMSG_Q	LD	A,1
	LD	(SCAN_ABORT),A
	RET
MMSG_3
	LD	HL,M_MOVING
	CALL	MESS
	LD	HL,(MSG_NUM)
	CALL	PRINT_NUMB
	CALL	PUTCR
;allow chance to abort.
	CALL	GET_$2
	CP	1
	JR	Z,MMSG_Q
;move message.
	CALL	TEXT_POSN
;
;Change message topic to number found
	LD	A,(FTN_TOP)
	LD	(HDR_TOPIC),A
;
;At this point the topic number is changed
;Anywhere the number appears it should be changed
; 1) NOT Text file -
; 2) Header file       *DONE*
; 3) Topic file (and in memory).   *DONE*
;
	LD	DE,MSG_TOPIC	;change in memory
	LD	HL,(A_MSG_POSN)
	ADD	HL,DE
	LD	A,(FTN_TOP)
	LD	(HL),A
	LD	HL,(A_MSG_POSN)
	LD	C,L
	LD	L,H
	LD	H,0
	LD	DE,16		;Offset 16 sectors.
	ADD	HL,DE
	LD	DE,TOP_FCB
	CALL	DOS_POS_RBA
	JP	NZ,ERROR
	LD	A,(FTN_TOP)
	CALL	$PUT
	JP	NZ,ERROR
;
;rewrite header.
	CALL	WRITE_MSG_HDR
;
;finished.
	LD	HL,M_MSGMVD
	CALL	MESS
	RET
;
MOVMSG_CMD
;
;***
MMGX_0
	LD	HL,M_MOVWHR	;move to where?
	CALL	GET_STRING
	LD	IX,TOPNAM_BUFF
MMGX_1
	CALL	GET_CHAR
	LD	(IX),A
	INC	IX
	CP	CR
	JR	NZ,MMGX_1
;
	LD	(IX-1),0
	LD	HL,TOPNAM_BUFF
	LD	A,(HL)
	OR	A
	JP	Z,MAIN
	CALL	FIND_TOP_NUM
	JR	NZ,MMGX_0
MMGX_4
	LD	HL,MOVEMESSAGE
	LD	(FUNCTION),HL
	LD	HL,M_MOVEMSG
	LD	(FUNCNM),HL
	CALL	DO_SCAN_1
	CALL	INFO_SETUP
	JP	SPEC_CMD
;
;Treewalk: Quasi-recursive tree scan to read Unread msgs
TREAD_CMD
	LD	HL,M_TREEWALK
	CALL	MESS
;
	LD	A,(OPTIONS)
	LD	(TR_OPTIONS),A
	SET	FO_CURR,A	;Turn off option 2
	RES	FO_LOWR,A
	LD	(OPTIONS),A
;
	LD	A,(MY_TOPIC)
	LD	(TR_TOPIC),A
	XOR	A		;Go to General
	LD	(MY_TOPIC),A
;
	LD	A,(MY_LEVEL)
	LD	(TR_LEVEL),A
	XOR	A		;Go to general
	LD	(MY_LEVEL),A
;
	CALL	INFO_SETUP	;Move yo' asses!
;
	LD	HL,TOP_RMESSAGE
	LD	(FUNCTION),HL	;Funky!
	LD	HL,1
	LD	(FIRST_MSG),HL	;From 1
	LD	HL,(N_MSG_TOP)
	LD	(LAST_MSG),HL	;To   $
;
	LD	A,4
	LD	(SCAN_MASK),A
;
TOP_LOOP:
	LD	A,1
	LD	(TR_NEWFLAG),A
	XOR	A
	LD	(TR_SKIP),A
	LD	(MSG_FOUND),A
	LD	(SCAN_ABORT),A
	CALL	DO_SCAN		;Scan all this topic
;
	LD	A,(TR_SKIP)	;Takes precedence over
	OR	A		;scan_abort for topic
	JR	NZ,TL_00	;skip.
	LD	A,(SCAN_ABORT)
	OR	A
	JR	NZ,TL_01
;
TL_00
	LD	A,(MY_TOPIC)
	CALL	TREE_WALK
	CP	0		;If wrap to general
	JR	Z,TL_01
	LD	(MY_TOPIC),A
	CALL	SUB_NONEX	;Check if used.
	JR	Z,TL_00		;If unused
	LD	A,(MY_TOPIC)
	CALL	INFO_SETUP
;(My_level remains at zero. Should have no effect?)
	LD	HL,1
	LD	(FIRST_MSG),HL	;From 1
	LD	HL,(N_MSG_TOP)
	LD	(LAST_MSG),HL	;To   $
;
	JR	TOP_LOOP
;
TL_01
	LD	A,(TR_OPTIONS)
	LD	(OPTIONS),A
	LD	A,(TR_TOPIC)
	LD	(MY_TOPIC),A
	LD	A,(TR_LEVEL)
	LD	(MY_LEVEL),A
	CALL	INFO_SETUP
	JP	MAIN		;Finished.
;
TOP_RMESSAGE
	LD	A,(TR_NEWFLAG)
	OR	A
	JR	Z,TRM_01
;
;** print topic name
	CALL	PUTCR
	LD	A,(MY_TOPIC)
	CALL	TOPIC_PRINT
	CALL	PUTCR
	CALL	PUTCR
;
;** print line of dashes
	XOR	A
	LD	(TR_NEWFLAG),A
;
TRM_01
	CALL	PUTCR
	CALL	PUTCR
	CALL	TEXT_POSN
	CALL	HDR_PRNT
;
TRM_02	CALL	BGETC
	OR	A
	JR	Z,TRM_08	;End of message
	CALL	PUT
;
	CALL	GET_$2
	OR	A
	JR	Z,TRM_02	;Nothing typed
	CALL	TRM_03
	JR	TRM_02		;Optional return
;
TRM_03
	AND	5FH
	CP	'N'
	JR	Z,TRM_06	;Skip rest of message
	CP	'T'
	JR	Z,TRM_07	;Skip rest of topic
	CP	'Q'
	JR	Z,TRM_05	;Quit the Treewalk
	RET			;Optional return
;
TRM_05	LD	A,1
	LD	(SCAN_ABORT),A
TRM_06
	CALL	PUTCR
	POP	BC		;Pop trm_03 return addr
	RET
TRM_07	LD	A,1
	LD	(SCAN_ABORT),A
	LD	(TR_SKIP),A
	JR	TRM_06
;
TRM_08
	LD	HL,M_TRMPAUSE
	CALL	MESS
TRM_09	CALL	GET_$2
	OR	A
	JR	Z,TRM_09
	CALL	TRM_03		;Handle key
	JR	TRM_09		;Optional return
;
TREE_WALK
	LD	B,A
	CP	0
	JR	NZ,S_TW_01
	ADD	A,32		;from gen to gen>sig
	RET
S_TW_01
	LD	A,1FH
	AND	B
	JR	NZ,S_TW_02
	LD	A,4
	ADD	A,B		;from gen>sig to gen>sig>c
	RET
S_TW_02
	LD	A,B
	INC	A
	RET
;
;End of bb3
