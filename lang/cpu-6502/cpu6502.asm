;CPU6502/MAC: EDTASM-PLUS MACRO 6502 EMULATOR...
; (C) 03/02/84 BY NICK ANDREW.
; THE 6502 CPU ACCEPTS THE FOLLOWING MNEMONICS:
;ADC(XADC),AND (XAND),ASL,BCC,BCS,BEQ,BIT (XBIT),BMI,BNE,
;BPL,BRK,BVC,BVS,CLC,CLD,CLI,CLV,CMP,CPX,CPY,DEC (XDEC),
;DEX,DEY,EOR,INC (XINC),INX,INY,JMP,JSR,LDA,LDX,LDY,LSR,
;NOP (XNOP),ORA,PHA,PHP,PLA,PLP,ROL,ROR,RTI,RTS,
;SBC (XSBC),SEC,SED,SEI,STA,STX,STY,TAX,TAY,TSX,TXA,
;TXS,TYA
CPU	LD	IX,AREG
	JP	PROGRM
AREG	DEFB	0
XREG	DEFB	0
YREG	DEFB	0
PREG	DEFB	0
SREG	DEFB	0FFH
PAGE0	DEFS	256
STACK	DEFS	256
ERROR	EX	AF,AF'
	PUSH	BC
	LD	BC,0123H
	PUSH	BC
	POP	AF
	POP	BC
	EX	AF,AF'
	JP	4386H
REVZ	JR	Z,REVV01
	CP	A
	RET
REVV01	CP	0
	RET	NZ
	CP	1
	RET
FLAGCC	BIT	0,(IX+3)
	RET
FLAGCS	BIT	0,(IX+3)
	CALL	REVZ
	RET
FLAGVC	BIT	6,(IX+3)
	RET
FLAGVS	BIT	6,(IX+3)
	CALL	REVZ
	RET
FLAGEQ	BIT	1,(IX+3)
	CALL	REVZ
	RET
FLAGNE	BIT	1,(IX+3)
	RET
FLAGPL	BIT	7,(IX+3)
	RET
FLAGMI	BIT	7,(IX+3)
	CALL	REVZ
	RET
PR1	LD	A,D
	OR	A
	JR	Z,PR1WRD
	DEC	A
	JR	Z,PR1X
	DEC	A
	JR	Z,PR1Y
	LD	A,H
	OR	A
	JP	NZ,ERROR
	LD	B,L
	RET
PR1WRD	CALL	PAGEHL
	LD	B,(HL)
	RET
PR1X	LD	C,E
	LD	A,C
	OR	A
	JR	Z,PR1V01
	LD	A,H
	OR	A
	JP	NZ,ERROR
PR1V01	LD	E,(IX+1)
	LD	D,0
	ADD	HL,DE
	LD	A,C
	OR	A
	JR	Z,PR1V02
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
PR1V02	CALL	PAGEHL
	LD	B,(HL)
	RET
PR1Y	LD	C,E
	LD	A,C
	OR	A
	JR	Z,PR1V04
	LD	A,H
	OR	A
	JP	NZ,ERROR
	CALL	PAGEHL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
PR1V04	LD	E,(IX+2)
	LD	D,0
	ADD	HL,DE
	CALL	PAGEHL
	LD	B,(HL)
	RET
PAGEHL	PUSH	AF
	LD	A,H
	CP	2
	JR	NC,PAGV01
	PUSH	DE
	LD	DE,PAGE0
	ADD	HL,DE
	POP	DE
PAGV01	POP	AF
	RET
PSHHL	EX	DE,HL
	LD	C,(IX+4)
	LD	B,1
	PUSH	BC
	POP	HL
	DEC	C
	CALL	PAGEHL
	LD	(HL),D
	PUSH	BC
	POP	HL
	CALL	PAGEHL
	LD	(HL),E
	DEC	C
	LD	(IX+4),C
	RET
PLLHL	LD	C,(IX+4)
	LD	B,1
	PUSH	BC
	POP	HL
	INC	C
	CALL	PAGEHL
	LD	E,(HL)
	PUSH	BC
	POP	HL
	CALL	PAGEHL
	LD	D,(HL)
	INC	C
	LD	(IX+4),C
	EX	DE,HL
	RET
PSHE	LD	C,(IX+4)
	LD	B,1
	PUSH	BC
	POP	HL
	CALL	PAGEHL
	LD	(HL),E
	DEC	C
	LD	(IX+4),C
	RET
PLLE	LD	C,(IX+4)
	LD	B,1
	INC	C
	PUSH	BC
	POP	HL
	CALL	PAGEHL
	LD	E,(HL)
	LD	(IX+4),C
	RET
PFLAGS	PUSH	AF
	POP	BC
	LD	A,(PREG)
	AND	3CH
	BIT	7,C
	JR	Z,PFLV01
	OR	80H
PFLV01	BIT	6,C
	JR	Z,PFLV02
	OR	02H
PFLV02	BIT	2,C
	JR	Z,PFLV03
	OR	40H
PFLV03	BIT	0,C
	JR	Z,PFLV04
	OR	01H
PFLV04	LD	(PREG),A
	RET
FLAGNZ	PUSH	AF
	PUSH	AF
	POP	BC
	LD	A,(PREG)
	AND	7DH
	BIT	7,C
	JR	Z,FLAV01
	OR	80H
FLAV01	BIT	6,C
	JR	Z,FLAV02
	OR	02H
FLAV02	LD	(PREG),A
	POP	AF
	RET
FLAGC	PUSH	AF
	PUSH	AF
	POP	BC
	LD	A,(PREG)
	AND	0FEH
	BIT	0,C
	JR	Z,FLAV11
	OR	1
FLAV11	LD	(PREG),A
	POP	AF
	RET
BCC	MACRO	#A1
	CALL	FLAGCC
	JP	Z,#A1
	ENDM
BCS	MACRO	#A1
	CALL	FLAGCS
	JP	Z,#A1
	ENDM
BEQ	MACRO	#A1
	CALL	FLAGEQ
	JP	Z,#A1
	ENDM
BMI	MACRO	#A1
	CALL	FLAGMI
	JP	Z,#A1
	ENDM
BNE	MACRO	#A1
	CALL	FLAGNE
	JP	Z,#A1
	ENDM
BPL	MACRO	#A1
	CALL	FLAGPL
	JP	Z,#A1
	ENDM
BRK	MACRO
	SET	4,(IX+3)
	JP	4386H
	ENDM
BVC	MACRO	#A1
	CALL	FLAGVC
	JP	Z,#A1
	ENDM
BVS	MACRO	#A1
	CALL	FLAGVS
	JP	Z,#A1
	ENDM
CLC	MACRO
	RES	0,(IX+3)
	ENDM
CLD	MACRO
	RES	3,(IX+3)
	ENDM
CLI	MACRO
	RES	2,(IX+3)
	ENDM
CLV	MACRO
	RES	6,(IX+3)
	ENDM
DEX	MACRO
	DEC	(IX+1)
	CALL	FLAGNZ
	ENDM
DEY	MACRO
	DEC	(IX+2)
	CALL	FLAGNZ
	ENDM
INX	MACRO
	INC	(IX+1)
	CALL	FLAGNZ
	ENDM
INY	MACRO
	INC	(IX+2)
	CALL	FLAGNZ
	ENDM
JMP	MACRO	#A1,#A2
A2I	DEFL	1
A2	DEFL	0
	LD	HL,#A1
	LD	A,A2#A2
	OR	A
	JP	Z,#A1
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)
	ENDM
JSR	MACRO	#A1
	LD	HL,RETURN
	PUSH	HL
	LD	HL,AA#$YM
	CALL	PSHHL
	JP	#A1
AA#$YM	EQU	$
	ENDM
XNOP	MACRO
	NOP
	ENDM
PHA	MACRO
	LD	E,(IX+0)
	CALL	PSHE
	ENDM
PHP	MACRO
	LD	E,(IX+3)
	CALL	PSHE
	ENDM
PLA	MACRO
	CALL	PLLE
	LD	A,E
	LD	(AREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
PLP	MACRO
	CALL	PLLE
	LD	(IX+3),E
	ENDM
RTI	MACRO
	CALL	PLLE
	LD	(IX+3),E
	CALL	PLLHL
	JP	(HL)
	ENDM
RTS	MACRO
	POP	HL
	CALL	PLLHL
	JP	(HL)
	ENDM
RETURN	CALL	PLLHL
	JP	(HL)
SEC	MACRO
	SET	0,(IX+3)
	ENDM
SED	MACRO
	SET	3,(IX+3)
	ENDM
SEI	MACRO
	SET	2,(IX+3)
	ENDM
TAX	MACRO
	LD	A,(AREG)
	LD	(XREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
TAY	MACRO
	LD	A,(AREG)
	LD	(YREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
TSX	MACRO
	LD	A,(SREG)
	LD	(XREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
TXA	MACRO
	LD	A,(XREG)
	LD	(AREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
TXS	MACRO
	LD	A,(XREG)
	LD	(SREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
TYA	MACRO
	LD	A,(YREG)
	LD	(AREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
XADC	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	BIT	0,(IX+3)
	SCF
	JR	NZ,AA#$YM
	CCF
AA#$YM	ADC	A,B
	LD	B,A
	PUSH	AF
	BIT	3,(IX+3)
	JR	Z,AB#$YM
	DAA
	LD	B,A
AB#$YM	POP	AF
	LD	A,B
	LD	(AREG),A
	CALL	PFLAGS
	ENDM
XSBC	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	BIT	0,(IX+3)
	SCF
	JR	Z,AA#$YM
	CCF
AA#$YM	SBC	A,B
	LD	B,A
	PUSH	AF
	BIT	3,(IX+3)
	JR	Z,AB#$YM
	DAA
	LD	B,A
AB#$YM	POP	AF
	LD	A,B
	LD	(AREG),A
	CCF
	CALL	PFLAGS
	ENDM
XAND	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	AND	B
	LD	(AREG),A
	CALL	FLAGNZ
	ENDM
CMP	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	CP	B
	CALL	FLAGC
	CALL	FLAGNZ
	ENDM
EOR	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	XOR	B
	LD	(AREG),A
	CALL	FLAGNZ
	ENDM
LDA	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,B
	LD	(AREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
ORA	MACRO	#A1,#A2,#A3
A0X	DEFL	1
A0Y	DEFL	2
A0B	DEFL	3
A0	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A0#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	OR	B
	LD	(AREG),A
	CALL	FLAGNZ
	ENDM
STA	MACRO	#A1,#A2,#A3
A3X	DEFL	1
A3Y	DEFL	2
A3	DEFL	0
A1	DEFL	0
A1I	DEFL	1
	LD	HL,#A1
	LD	D,A3#A2
	LD	E,A1#A3
	CALL	PR1
	LD	A,(AREG)
	LD	(HL),A
	ENDM
XBIT	MACRO	#A1
	LD	HL,#A1
	LD	DE,0
	CALL	PR1
	LD	A,(HL)
	AND	0C0H
	OR	(IX+3)
	AND	0FDH
	LD	(IX+3),A
	LD	A,(AREG)
	AND	(HL)
	LD	A,(PREG)
	JR	NZ,AA#$YM
	OR	2
AA#$YM	LD	(PREG),A
	ENDM
ASL	MACRO	#A1,#A2
A4X	DEFL	1
A4A	DEFL	0
A4	DEFL	0
AXA	DEFL	1
AX	DEFL	0
	LD	HL,#A1
	LD	D,A4#A2
	LD	E,0
	CALL	PR1
	LD	A,AX#A2
	CP	1
	JR	NZ,AA#$YM
	LD	HL,AREG
AA#$YM	SLA	(HL)
	CALL	FLAGNZ
	CALL	FLAGC
	ENDM
LSR	MACRO	#A1,#A2
A4X	DEFL	1
A4A	DEFL	0
A4	DEFL	0
AXA	DEFL	1
AX	DEFL	0
	LD	HL,#A1
	LD	D,A4#A2
	LD	E,0
	CALL	PR1
	LD	A,AX#A2
	CP	1
	JR	NZ,AA#$YM
	LD	HL,AREG
AA#$YM	SRL	(HL)
	CALL	FLAGNZ
	CALL	FLAGC
	RES	7,(IX+3)
	ENDM
ROL	MACRO	#A1,#A2
A4X	DEFL	1
A4A	DEFL	0
A4	DEFL	0
AXA	DEFL	1
AX	DEFL	0
	LD	HL,#A1
	LD	D,A4#A2
	LD	E,0
	CALL	PR1
	LD	A,AX#A2
	CP	1
	JR	NZ,AA#$YM
	LD	HL,AREG
AA#$YM	BIT	0,(IX+3)
	SCF
	JR	NZ,AB#$YM
	CCF
AB#$YM	RL	(HL)
	CALL	FLAGNZ
	CALL	FLAGC
	ENDM
ROR	MACRO	#A1,#A2
A4X	DEFL	1
A4A	DEFL	0
A4	DEFL	0
AXA	DEFL	1
AX	DEFL	0
	LD	HL,#A1
	LD	D,A4#A2
	LD	E,0
	CALL	PR1
	LD	A,AX#A2
	CP	1
	JR	NZ,AA#$YM
	LD	HL,AREG
AA#$YM	BIT	0,(IX+3)
	SCF
	JR	NZ,AB#$YM
	CCF
AB#$YM	RR	(HL)
	CALL	FLAGNZ
	CALL	FLAGC
	ENDM
XDEC	MACRO	#A1,#A2
A5	DEFL	0
A5X	DEFL	1
	LD	HL,#A1
	LD	D,A5#A2
	LD	E,0
	CALL	PR1
	DEC	(HL)
	CALL	FLAGNZ
	ENDM
XINC	MACRO	#A1,#A2
A5	DEFL	0
A5X	DEFL	1
	LD	HL,#A1
	LD	D,A5#A2
	LD	E,0
	CALL	PR1
	INC	(HL)
	CALL	FLAGNZ
	ENDM
CPX	MACRO	#A1,#A2
A6	DEFL	0
A6B	DEFL	3
	LD	HL,#A1
	LD	D,A6#A2
	LD	E,0
	CALL	PR1
	LD	A,(XREG)
	CP	B
	CALL	FLAGNZ
	CALL	FLAGC
	ENDM
CPY	MACRO	#A1,#A2
A6	DEFL	0
A6B	DEFL	3
	LD	HL,#A1
	LD	D,A6#A2
	LD	E,0
	CALL	PR1
	LD	A,(YREG)
	CP	B
	CALL	FLAGNZ
	CALL	FLAGC
	ENDM
LDX	MACRO	#A1,#A2
A7	DEFL	0
A7X	DEFL	1
A7B	DEFL	3
	LD	HL,#A1
	LD	D,A7#A2
	LD	E,0
	CALL	PR1
	LD	A,B
	LD	(XREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
LDY	MACRO	#A1,#A2
A8	DEFL	0
A8Y	DEFL	2
A8B	DEFL	3
	LD	HL,#A1
	LD	D,A8#A2
	LD	E,0
	CALL	PR1
	LD	A,B
	LD	(YREG),A
	OR	A
	CALL	FLAGNZ
	ENDM
STX	MACRO	#A1,#A2
A9	DEFL	0
A9Y	DEFL	2
	LD	HL,#A1
	LD	D,A9#A2
	LD	E,0
	CALL	PR1
	LD	A,(XREG)
	LD	(HL),A
	ENDM
STY	MACRO	#A1,#A2
AA	DEFL	0
AAX	DEFL	1
	LD	HL,#A1
	LD	D,AA#A2
	LD	E,0
	CALL	PR1
	LD	A,(YREG)
	LD	(HL),A
	ENDM
*LIST ON
PROGRM	NOP
;
;	Insert 6502 program here...
;	LDA	3,B
;
	END	CPU
